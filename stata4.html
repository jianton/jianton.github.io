<!DOCTYPE html>
<html xmlns="http://www.w3.org/1999/xhtml" lang="" xml:lang="">
<head>
<style>
/* CSS for Markstat 2.0 using Pandoc 2.0 */
body{padding:14px 28px;}
body, table {font-family: Helvetica, Arial, Sans-serif; font-size: 14px;}
h1, h2, h3, h4 {font-weight: normal; color: #3366cc}
h1 {font-size: 200%;}
h2 {font-size: 150%;}
h3 {font-size: 120%;}
h4 {font-size: 100%; font-weight:bold}
img.center {display:block; margin-left:auto; margin-right:auto}
.small{font-size:8pt;}
a {color: black;}
a:visited {color: #808080;}
a.plain {text-decoration:none;}
a.plain:hover {text-decoration:underline;}
.em {font-weight:bold;}
pre, code {font-family: "lucida console", monospace;}
pre.stata {font-size:13px; line-height:13px;}
pre {padding:8px; border:1px solid #c0c0c0; border-radius:8px; background-color:#fdfdfd;}
code {color:#3366cc; background-color:#fafafa;}
pre code { color:black; background-color:white}
/* Added for Pandoc */
figure > img, div.figure > img {display:block; margin:auto}
figcaption, p.caption {text-align:center; font-weight:bold; color:#3366cc;}
h1.title {text-align:center; margin-bottom:0}
p.author, h2.author {font-style:italic; text-align:center;margin-top:4px;margin-bottom:0}
p.date, h3.date {text-align:center;margin-top:4px; margin-bottom:0}
/* Tables*/
table { margin:auto; border-collapse:collapse; }
table caption { margin-bottom:1ex;}
th, td { padding:4px 6px;}
thead tr:first-child th {border-top:1px solid black; padding-top:6px}
thead tr:last-child  th {padding-bottom:6px}
tbody tr:first-child td {border-top:1px solid black; padding-top:6px}
tbody tr:last-child  td {padding-bottom:6px;}
table tbody:last-child tr:last-child td {border-bottom:1px solid black;}
</style>
  <meta charset="utf-8" />
  <meta name="generator" content="pandoc" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes" />
  <meta name="author" content="José-Ignacio Antón" />
  <title>Sesión 4. Análisis gráfico</title>
  <style>
    code{white-space: pre-wrap;}
    span.smallcaps{font-variant: small-caps;}
    span.underline{text-decoration: underline;}
    div.column{display: inline-block; vertical-align: top; width: 50%;}
    div.hanging-indent{margin-left: 1.5em; text-indent: -1.5em;}
    ul.task-list{list-style: none;}
  </style>
  <script src="https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-mml-chtml.js" type="text/javascript"></script>
  <!--[if lt IE 9]>
    <script src="//cdnjs.cloudflare.com/ajax/libs/html5shiv/3.7.3/html5shiv-printshiv.min.js"></script>
  <![endif]-->
</head>
<body>
<header id="title-block-header">
<h1 class="title">Sesión 4. Análisis gráfico</h1>
<p class="author">José-Ignacio Antón</p>
</header>
<hr />
<p style="text-align: center;">
<a href="https://jianton.github.io/curso_stata.html"><span style="color:red"><font size="4">Inicio</font></span></a>      <a href="https://jianton.github.io/stata1.html"><span style="color:red"><font size="4">Sesión 1</font></span></a>      <a href="https://jianton.github.io/stata2.html"><span style="color:red"><font size="4">Sesión 2</font></span></a>      <a href="https://jianton.github.io/stata3.html"><span style="color:red"><font size="4">Sesión 3</font></span></a>      <a href="https://jianton.github.io/stata4.html"><span style="color:red"><font size="4">Sesión 4</font></span></a>      <a href="https://jianton.github.io/stata5.html"><span style="color:red"><font size="4">Sesión 5</font></span></a>      <a href="https://jianton.github.io/stata_ejercicios.html"><span style="color:red"><font size="4">Ejercicios</font></span></a>
</p>
<hr />
<h1 id="consejos-preliminares">1. Consejos preliminares</h1>
<h2 id="línea-de-comandos-o-menús-no-tenemos-por-qué-elegir">¿Línea de comandos o menús? No tenemos por qué elegir</h2>
<p><code>Stata</code> presenta buenas capacidades gráficas y estas han ido mejorando a lo largo del tiempo de forma muy sustancial. En <span class="citation" data-cites="Escobar2012">Escobar et al. (2012)</span> se realiza una aproximación al tema y tres referencias muy recomendables son <span class="citation" data-cites="Cox2014">Cox (2014)</span> y <span class="citation" data-cites="Mitchell2012">Mitchell (2012)</span>, sobre la realización de gráficos con este paquete estadístico, y <span class="citation" data-cites="Schwabish2014">Schwabish (2014)</span>, que proporciona unos consejos generales sobre cómo realizar buenos gráficos.</p>
<p>En primer lugar, resulta muy sencillo realizar gráficos con <code>Stata</code>. Al mismo tiempo, elaborar figuras totalmente satisfactorias suele requerir bastante tiempo <span class="math inline">\(\rightarrow\)</span> <strong>resulta esencial que nuestros gráficos sean reproducibles</strong>.</p>
<p>Así, por ejemplo, no es complicado realizar gráficos muy sofisticados con la interfaz de <code>Stata</code>. Su sistema de ventanas es muy intuitivo y nos permite controlar múltiples opciones.</p>
<figure>
<img src="histogram.png" style="width:30.0%" alt="" /><figcaption>Ejemplo de uso del sistema de ventanas para elaborar un histograma</figcaption>
</figure>
<p>El problema radica en la <strong>reproducibilidad</strong>: si reiniciamos el análisis gráfico o salimos del programa, tendremos que acordarnos de todas las opciones seleccionadas y, además, tendremos que repetir los cambios realizados en el editor de gráficos sobre la propia figura. <span class="math inline">\(\rightarrow\)</span> <strong>Opción práctica:</strong> realizamos el gráfico con los menús de ventanas, empleando <code>submit</code> para ver cada cambio realizado y, cuando estemos satisfechos, copiamos el código y podemos reutilizarlo con adaptaciones.</p>
<h2 id="personalizando-gráficos">Personalizando gráficos</h2>
<p>Un avance sustancial viene dado por el paquete <code>grstyle</code>, que debe utilizarse conjuntamente <code>palettes</code> y <code>colrspace</code> <span class="citation" data-cites="Jann2018a">(Jann, 2018a, p. @Jann2018b)</span>. También es muy potente el paquete <code>brewscheme</code>. La idea de estos paquetes es que podemos definir previamente el formato de los gráficos (colores, ancho de las líneas, etc.) y, posteriormente, emplear comandos sencillos para la realización de los gráficos donde no tengamos que especificar muchas opciones.</p>
<pre class='stata'>. ssc install grstyle
checking grstyle consistency and verifying not already installed...
all files already exist and are up to date.

. ssc install palettes
checking palettes consistency and verifying not already installed...
all files already exist and are up to date.

. ssc install colrspace
checking colrspace consistency and verifying not already installed...
all files already exist and are up to date.

. ssc install brewscheme
checking brewscheme consistency and verifying not already installed...
all files already exist and are up to date.
</pre>
<p>Estos paquetes nos facilitan bastante el trabajo, pero es muy posible que queramos, en cada gráfico, añadir opciones específicas de personalización más allá de los parámetros generales.</p>
<h2 id="formatos-de-exportación">Formatos de exportación</h2>
<p><code>Stata</code> cuenta con un formato nativo <code>.gph</code> con bastante calidad y que puede ser una buena opción para guardar las figuras elaboradas con la posibilidad de modificarlas luego. Además, naturalmente, podemos copiar las figuras producidas en la ventana de gráficos y pegarlas en un documento de un procesador de textos (e.g., Microsoft Word) o un programa de edición de imágenes (e.g., Microsoft Paint) Sin embargo, existe un gran número de formatos en los que <code>Stata</code> puede guardar los graficos: <code>.ps</code>, <code>.eps</code>, <code>.svg</code>, <code>.wmf</code>, <code>.emf</code>, <code>.pdf</code>, <code>.png</code>, <code>.tif</code>, <code>.gif</code> y <code>jpg</code>. Formatos como <code>.wmf</code> y <code>.emf</code> son muy apropiados para aplicaciones de Microsoft Office (e.g., Microsoft Word). Sin embargo, los formatos de vectores asegura muchísima calidad y son los empleados en edición profesional (.ps<code>,</code>.eps<code>,</code>.svg<code>,</code>.pdf<code>,</code>.png`). La instrucción para exportar gráficos es la siguiente:</p>
<p><code>graph export <i>newfilename.suffix</i> [, <i>options</i>]</code></p>
<h1 id="gráficos-más-frecuentes-y-potencialidades-de-los-gráficos-de-stata">2. Gráficos más frecuentes y potencialidades de los gráficos de <code>Stata</code></h1>
<p>En primer lugar, vamos a configurar el formato de los gráficos para que nos sirva para todas las figuras que elaboraremos con el comando <code>grstryle</code>. Sin embargo, como se mencionaba anteriormente, de forma habitual, necesitaremos implementar opciones adicionales. En esta sección, vamos a tratar de elaborar gráficos de alta calidad aptos para publicaciones científicas siguiendo los formatos sugeridos por <span class="citation" data-cites="Schwabish2014">Schwabish (2014)</span> para Ciencias Sociales. Dado que el número de gráficos que <code>Stata</code> puede realizar no puede ser abarcado (siquiera lejanamente) en un curso de estas características, aquí nos limitamos a presentar una serie de figuras que ilustran su potencial. El proceso de realización de los mismos se fundamenta en procesos de ensayo-error y en emplear las herramientas de ayuda destacadas con anterioridad.</p>
<h2 id="gráfico-de-dispersión">Gráfico de dispersión</h2>
<p>Comenzamos por un gráfico sencillo, en el que tratamos de ver la relación el salario medio de una Comunidad Autónoma y la proporción de personas pobres (tasa de pobreza). Realizaremos en primer lugar el gráfico sin personalización alguna y, posteriormente, elaboraremos una figura con opciones personalizadas. El proceso es eminentemente <strong>learning by doing</strong> y la forma habitual de trabajo es elaborar el gráfico con los menús y obtener el código que responde a nuestras preferencias, que llevaremos al archivo <code>.do</code> para poder reproducir el gráfico cuando lo deseemos o reutilizarlo con otras bases de datos.</p>

<pre class='stata'>. //  Fijamos el directorio de trabajo
. 
. cd "D:\Dropbox\curso_stata\sesión4"
D:\Dropbox\curso_stata\sesión4

. 
. //  Abrimos el archivo
. 
. use "ecv18.dta", clear
(ECV 2016-2018)
</pre>
<pre class='stata'>. //  Calculamos el salario medio y la tasa de pobreza por CCAA
. 
. generate rentaasa = rentamonasag + rentamonasag
(5,362 missing values generated)

. generate nmesesasa = nmesesasatc + 0.5*nmesesasatp
(5,578 missing values generated)

. keep if rentaasa > 0 &amp; rentaasa &lt; .
(19,416 observations deleted)

. keep if nmesesasa > 0 &amp; nmesesasa &lt; .
(2,540 observations deleted)

. 
. generate salario = rentaasa/nmesesasa

. label variable salario "Salario bruto (euros/mes)"

. 
. //  Creamos la base de datos con los estadísticos de interés
. 
. collapse (mean) salario hpobreza (count) nper = pid [pweight=peso], by(region)

. 
. //  Expresamos la variable pobreza en tanto por 100
. 
. replace hpobreza = 100*hpobreza
(19 real changes made)

. 
. //  El patrón por defecto de Stata
. 
. twoway (scatter hpobreza salario), ///
> xtitle("Salario bruto (euros/mes)") ytitle("Porcentaje de personas pobres") ///
> title("Salario y pobreza por CCAA (2018)")
</pre>

<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\example.png" style="width:40.0%" alt="" /><figcaption>Gráfico por defecto</figcaption>
</figure>
<p>Ahora vamos a realizar el mismo gráfico de una manera mucho más personalizada.</p>
<pre class='stata'>. //  Podemos cambiar el tipo de letra
. 
. graph set window fontface       "Times New Roman"

. graph set window fontfacemono   "Times New Roman"

. graph set window fontfacesans   "Times New Roman"

. graph set window fontfaceserif  "Times New Roman"

. graph set window fontfacesymbol "Symbol"

. 
. //  Comenzamos fijando un tipo de gráfico como base o eliminar todo esa base
. 
. grstyle clear

. grstyle init

. grstyle set plain

. 
. //  Fijamos un fondo blanco
. 
. grstyle color background white

. 
. //  Definimos las dimensiones del gráfico
. 
. grstyle graphsize x 4

. grstyle graphsize y 4

. 
. //  Podemos eliminar las líneas auxiliares
. 
. grstyle set nogrid

. 
. //  Seleccionamos una paleta de colores (e.g., paleta hue)
. 
. grstyle set color hue, n(4)

. 
. //  Eliminamos la línea que rodea a la leyenda
. 
. grstyle linestyle legend none

. 
. //  Ponemos la leyenda arriba
. 
. grstyle set legend 6

. 
. //  Formato de los marcadores
. 
. grstyle symbol p circle_hollow

. 
. //  Color de los marcadores
. 
. grstyle color p1markline gs10

. 
. grstyle color p1markfill gs10

. 
. //  Color de las series
. 
. grstyle set color red green orange blue

. 
. //  Gráfico de ejemplo
. 
. twoway (scatter hpobreza salario [pweight = nper] if region != 9) ///
> (scatter hpobreza salario [pweight = nper] if region == 9, mcolor(red)) ///
> (scatter hpobreza salario [pweight = nper] if region == 9, mlabel(region) mlabsize(vsmall) mlabcolor(red) mlabposition(12) mlabgap(medsma
> ll) mcolor(none)) ///
> (lfit hpobreza salario [pweight = nper]), ///
> title("Salario y pobreza por CCAA (2018)", size(medsmall) margin(small)) ///
> xtitle("Salario bruto (euros/mes)", margin(small) size(small)) xlabel(, format(%9,0gc) labsize(small)) ///
> ytitle("Porcentaje de personas pobres (%)", margin(small) size(small)) ylabel(, format(%2,0fc) labsize(small)) ///
> legend(off)
</pre>

<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\scatter.png" style="width:40.0%" alt="" /><figcaption>Gráfico de dispersión (personalizado)</figcaption>
</figure>
<h2 id="gráfico-de-líneas">Gráfico de líneas</h2>
<p>En este ejemplo, vamos a representar el salario por grupos de edad y nivel de estudios.</p>
<pre class='stata'>. //  Abrimos el archivo
. 
. use "ecv18.dta", clear
(ECV 2016-2018)

. 
. //  Calculamos la edad y los grupos de edad
. 
. generate edad = 2018 - anonac

. generate edadg = 1 if edad > 16 &amp; edad &lt;= 29
(29,586 missing values generated)

. replace edadg = 2 if edad >= 30 &amp; edad &lt;= 44
(6,145 real changes made)

. replace edadg = 3 if edad >= 45 &amp; edad &lt;= 54
(5,361 real changes made)

. replace edadg = 4 if edad >= 55 &amp; edad &lt;= 64
(5,159 real changes made)

. label variable edadg "Grupos de edad"

. label define edadg_lab 1 "16-29" 2 "30-44" 3 "45-54" 4 "55-64"

. label values edadg edadg_lab

. 
. //  Calculamos grupos de niveles educativos
. 
. generate educg = 1 if educ == 0 | educ == 100
(27,003 missing values generated)

. replace educg = 2 if educ == 200 | educ == 344 | educ == 353
(8,725 real changes made)

. replace educg = 3 if educ == 300 | educ == 354 | educ == 400 | educ == 450
(4,735 real changes made)

. replace educg = 4 if educ == 500
(7,969 real changes made)

. 
. label variable educg "Educación (agrupada)"

. label define educg_lab 1 "Elemental" 2 "Básica" 3 "Media" 4 "Superior"

. label values educg educg_lab

. 
. //  Calculamos el salario
. 
. generate rentaasa = rentamonasag + rentamonasag
(5,362 missing values generated)

. generate nmesesasa = nmesesasatc + 0.5*nmesesasatp
(5,578 missing values generated)

. keep if rentaasa > 0 &amp; rentaasa &lt; .
(19,416 observations deleted)

. keep if nmesesasa > 0 &amp; nmesesasa &lt; .
(2,540 observations deleted)

. 
. generate salario = rentaasa/nmesesasa

. label variable salario "Salario bruto (euros/mes)"

. 
. //  Eliminamos valores perdidos
. 
. drop if educg == . | edadg == . | salario == .
(177 observations deleted)

. 
. //  Creamos la base de datos con los estadísticos de interés
. 
. collapse (mean) salario [pweight=peso], by(edadg educg)

. 
. //  Realizamos algunas manipulaciones para representar los datos
. 
. expand 5
(64 observations created)

. 
. bysort edadg educg: generate copy = _n

. label variable copy "Copy for graph"

. label define copy_lab 1 "Elemental" 2 "Básica" 3 "Media" 4 "Superior"

. label values copy copy_lab

. 
. forvalues i = 1(1)4 {
  2.   drop if educg == `i' &amp; copy == `i'
  3. 
. }
(4 observations deleted)
(4 observations deleted)
(4 observations deleted)
(4 observations deleted)

. 
. bysort edadg educg: egen meducg =mean(educg)

. 
. replace educg = 5 if copy == 5
(16 real changes made)

. 
. replace copy = meducg if copy == 5
(16 real changes made)

. 
. //  Realizamos el gráfico de interés
. 
. twoway ///
> (line salario edadg if educg == 1, lcolor(gs10)) ///
> (line salario edadg if educg == 2, lcolor(gs10)) ///
> (line salario edadg if educg == 3, lcolor(gs10)) ///
> (line salario edadg if educg == 4, lcolor(gs10)) ///
> (line salario edadg if educg == 5, lcolor(blue)), ///
> ytitle("Salario mensual", size(small) margin(small)) ylabel(, labsize(small) format(%5,0fc)) ///
> xtitle("Edad", size(small) margin(small)) xlabel(1 "16-29" 2 "30-44" 3 "45-54" 4 "55-64", labsize(small)) ///
> by(copy, note("") legend(off) imargin(medsmall) title("Salario por edad y nivel educativo (2018)", size(medsmall) margin(small))) ///
> subtitle(, size(small)) ///
> xsize(4) ysize(4)
</pre>

<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\lineplot.png" style="width:40.0%" alt="" /><figcaption>Gráfico de líneas</figcaption>
</figure>
<h2 id="gráfico-de-puntos">Gráfico de puntos</h2>
<p>En este subapartado, elaboramos una figura que muestra la tasa de empleo por sexo y nivel educativo en España.</p>
<pre class='stata'>. //  Abrimos el archivo
. 
. use "ecv18.dta", clear
(ECV 2016-2018)

. 
. //  Calculamos la edad
. 
. generate edad = 2018 - anonac

. 
. //  Calculamos la tasa de empleo
. 
. gen empleo = 0 if edad >= 18 &amp; edad &lt;= 64
(13,265 missing values generated)

. recode empleo (0 = 1) if sitact == 1
(empleo: 12867 changes made)

. 
. //  Calculamos los grupos de niveles educativos
. 
. generate educg = 1 if educ == 0 | educ == 100
(27,003 missing values generated)

. replace educg = 2 if educ == 200 | educ == 344 | educ == 353
(8,725 real changes made)

. replace educg = 3 if educ == 300 | educ == 354 | educ == 400 | educ == 450
(4,735 real changes made)

. replace educg = 4 if educ == 500
(7,969 real changes made)

. 
. label variable educg "Educación (agrupada)"

. label define educg_lab 1 "Educación elemental" 2 "Educación básica" 3 "Educación media" 4 "Educación superior"

. label values educg educg_lab

. 
. keep if empleo &lt; . &amp; educg &lt; .
(13,442 observations deleted)

. 
. //  Creamos la base de datos con los estadísticos de interés
. 
. collapse (mean) empleo [pweight=peso], by(sexo educg)

. 
. //  Expresamos la tasa de empleo en tanto por 100
. 
. replace empleo = 100*empleo
(8 real changes made)

. 
. //  Elaboramos el gráfico de interés
. 
. twoway ///
> (line educg empleo if educg == 1, lcolor(black)) ///
> (line educg empleo if educg == 2, lcolor(black)) ///
> (line educg empleo if educg == 3, lcolor(black)) ///
> (line educg empleo if educg == 4, lcolor(black)) ///
> (scatter educg empleo if sex == 1, mcolor(navy) msize(4-pt) msymbol(circle) mlabcolor()) ///
> (scatter educg empleo if sexo == 2, mcolor(orange) msize(4-pt) msymbol(circle) mlabel(educg) mlabsize(small) mlabcolor(black) mlabpositio
> n(9) mlabgap(small)), ///
> title("Tasa de empleo por sexo y nivel educativo en España (2018)", size(medsmall) margin(small)) ///
> yscale(off) ylabel(0(1)4) ///
> xtitle("Porcentaje de personas empleadas (%)", margin(small) size(small)) xlabel(0(20)100, labsize(vsmall)) ///
> legend(on order(5 "Hombres" 6 "Mujeres") size(vsmall) color(black) nobox position(11))
</pre>

<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\dotplot.png" style="width:40.0%" alt="" /><figcaption>Gráfico de puntos</figcaption>
</figure>
<h2 id="gráfico-de-barras">Gráfico de barras</h2>
<p>A continuación, mostramos la tasa de pobreza por nivel educativo y sexo en España en el año 2018. Además, en este ejemplo, vamos a usar el editor de gráficos de <code>Stata</code>.</p>
<pre class='stata'>. //  Abrimos el archivo
. 
. use "ecv18.dta", clear
(ECV 2016-2018)

. 
. //  Calculamos los grupos de niveles educativos
. 
. generate educg = 1 if educ == 0 | educ == 100
(27,003 missing values generated)

. replace educg = 2 if educ == 200 | educ == 344 | educ == 353
(8,725 real changes made)

. replace educg = 3 if educ == 300 | educ == 354 | educ == 400 | educ == 450
(4,735 real changes made)

. replace educg = 4 if educ == 500
(7,969 real changes made)

. 
. label variable educg "Educación (agrupada)"

. label define educg_lab 1 "Educación elemental" 2 "Educación básica" 3 "Educación media" 4 "Educación superior"

. label values educg educg_lab

. 
. keep if educg &lt; .
(5,574 observations deleted)

. 
. //  Creamos la base de datos con los estadísticos de interés
. 
. collapse (mean) hpobreza [pweight=peso], by(sexo educg)

. 
. //  Expresamos la tasa de pobreza en tanto por 100
. 
. replace hpobreza = 100*hpobreza
(8 real changes made)

. 
. //  Reordenamos los datos para representarlos
. 
. reshape wide hpobreza, i(educg) j(sexo)
(note: j = 1 2)

Data                               long   ->   wide
─────────────────────────────────────────────────────────────────────────────
Number of obs.                        8   ->       4
Number of variables                   3   ->       3
j variable (2 values)              sexo   ->   (dropped)
xij variables:
                               hpobreza   ->   hpobreza1 hpobreza2
─────────────────────────────────────────────────────────────────────────────

. 
. //  Elaboramos el gráfico de interés
. 
. graph bar (asis) hpobreza1 hpobreza2, ///
> over(educg, relabel(1 "Elementales" 2 "Básicos" 3 "Medios" 4 "Superiores") label(labsize(vsmall))) ///
> bar(1, fcolor(navy) lcolor(none)) ///
> bar(2, fcolor(orange) lcolor(none)) ///
> ytitle(Tasa de pobreza (%)) ytitle(, size(vsmall) margin(sides)) ylabel(0(10)50, labsize(vsmall) nogrid) ///
> title(Pobreza por sexo y nivel educativo en España (2018), size(medsmall) margin(top_bottom)) legend(off) ///
> b1title("Nivel de estudios", size(vsmall) margin(top_bottom))

. 
. gr_edit plotregion1.AddTextBox added_text editor 35.8934560474294 10.62503501939819

. gr_edit plotregion1.added_text_new = 1

. gr_edit plotregion1.added_text_rec = 1

. gr_edit plotregion1.added_text[1].style.editstyle  angle(default) size( sztype(points) val(2) allow_pct(1)) color(navy) horizontal(left) 
> vertical(middle) margin( gleft( sztype(relative) val(0) allow_pct(1)) gright( sztype(relative) val(0) allow_pct(1)) gtop( sztype(relative
> ) val(0) allow_pct(1)) gbottom( sztype(relative) val(0) allow_pct(1))) linegap( sztype(relative) val(0) allow_pct(1)) drawbox(no) boxmarg
> in( gleft( sztype(relative) val(0) allow_pct(1)) gright( sztype(relative) val(0) allow_pct(1)) gtop( sztype(relative) val(0) allow_pct(1)
> ) gbottom( sztype(relative) val(0) allow_pct(1))) fillcolor(bluishgray) linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(bl
> ack) pattern(solid) align(inside)) box_alignment(east) editcopy

. gr_edit plotregion1.added_text[1].style.editstyle size(vsmall) editcopy

. gr_edit plotregion1.added_text[1].text = {}

. gr_edit plotregion1.added_text[1].text.Arrpush Hombres

. 
. gr_edit plotregion1.AddTextBox added_text editor 32.6954873184468 19.95200785068754

. gr_edit plotregion1.added_text_new = 2

. gr_edit plotregion1.added_text_rec = 2

. gr_edit plotregion1.added_text[2].style.editstyle  angle(default) size( sztype(relative) val(4.1667) allow_pct(1)) color(navy) horizontal
> (left) vertical(middle) margin( gleft( sztype(relative) val(0) allow_pct(1)) gright( sztype(relative) val(0) allow_pct(1)) gtop( sztype(r
> elative) val(0) allow_pct(1)) gbottom( sztype(relative) val(0) allow_pct(1))) linegap( sztype(relative) val(0) allow_pct(1)) drawbox(no) 
> boxmargin( gleft( sztype(relative) val(0) allow_pct(1)) gright( sztype(relative) val(0) allow_pct(1)) gtop( sztype(relative) val(0) allow
> _pct(1)) gbottom( sztype(relative) val(0) allow_pct(1))) fillcolor(bluishgray) linestyle( width( sztype(relative) val(.2) allow_pct(1)) c
> olor(black) pattern(solid) align(inside)) box_alignment(east) editcopy

. gr_edit plotregion1.added_text[2].style.editstyle size(vsmall) editcopy

. gr_edit plotregion1.added_text[2].text = {}

. gr_edit plotregion1.added_text[2].text.Arrpush Mujeres

. gr_edit plotregion1.added_text[2].style.editstyle color(orange) editcopy

. 
. gr_edit plotregion1.AddLine added_lines editor 6.412853740751393 30.4815089676127 6.412853740751393 35.64745845289226

. gr_edit plotregion1.added_lines_new = 1

. gr_edit plotregion1.added_lines_rec = 1

. gr_edit plotregion1.added_lines[1].style.editstyle  linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(navy) pattern(solid) a
> lign(inside)) headstyle( symbol(circle) linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(navy) pattern(solid) align(inside)
> ) fillcolor(navy) size( sztype(relative) val(1.04166) allow_pct(1)) angle(stdarrow) symangle(zero) backsymbol(none) backline( width( szty
> pe(relative) val(.2) allow_pct(1)) color(navy) pattern(solid) align(inside)) backcolor(navy) backsize( sztype(relative) val(0) allow_pct(
> 1)) backangle(stdarrow) backsymangle(zero)) headpos(neither) editcopy

. 
. gr_edit plotregion1.AddLine added_lines editor 6.412853740751393 35.56545925471323 9.872859791068407 35.56545925471323

. gr_edit plotregion1.added_lines_new = 2

. gr_edit plotregion1.added_lines_rec = 2

. gr_edit plotregion1.added_lines[2].style.editstyle  linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(navy) pattern(solid) a
> lign(inside)) headstyle( symbol(circle) linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(navy) pattern(solid) align(inside)
> ) fillcolor(navy) size( sztype(relative) val(1.04166) allow_pct(1)) angle(stdarrow) symangle(zero) backsymbol(none) backline( width( szty
> pe(relative) val(.2) allow_pct(1)) color(black) pattern(solid) align(inside)) backcolor(black) backsize( sztype(relative) val(0) allow_pc
> t(1)) backangle(stdarrow) backsymangle(zero)) headpos(head) editcopy

. 
. gr_edit plotregion1.AddLine added_lines editor 15.89026161770668 27.44753863498822 15.89026161770668 32.2854913275516

. gr_edit plotregion1.added_lines_new = 3

. gr_edit plotregion1.added_lines_rec = 3

. gr_edit plotregion1.added_lines[3].style.editstyle  linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(orange) pattern(solid)
>  align(inside)) headstyle( symbol(circle) linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(orange) pattern(solid) align(ins
> ide)) fillcolor(orange) size( sztype(relative) val(1.04166) allow_pct(1)) angle(stdarrow) symangle(zero) backsymbol(none) backline( width
> ( sztype(relative) val(.2) allow_pct(1)) color(black) pattern(solid) align(inside)) backcolor(navy) backsize( sztype(relative) val(0) all
> ow_pct(1)) backangle(stdarrow) backsymangle(zero)) headpos(neither) editcopy

. 
. gr_edit plotregion1.AddLine added_lines editor 15.89026161770668 32.28549132755161 18.74852748535989 32.28549132755161

. gr_edit plotregion1.added_lines_new = 4

. gr_edit plotregion1.added_lines_rec = 4

. gr_edit plotregion1.added_lines[4].style.editstyle  linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(orange) pattern(solid)
>  align(inside)) headstyle( symbol(circle) linestyle( width( sztype(relative) val(.2) allow_pct(1)) color(orange) pattern(solid) align(ins
> ide)) fillcolor(orange) size( sztype(relative) val(1.04166) allow_pct(1)) angle(stdarrow) symangle(zero) backsymbol(none) backline( width
> ( sztype(relative) val(.2) allow_pct(1)) color(black) pattern(solid) align(inside)) backcolor(black) backsize( sztype(relative) val(0) al
> low_pct(1)) backangle(stdarrow) backsymangle(zero)) headpos(head) editcopy
</pre>

<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\barplot.png" style="width:40.0%" alt="" /><figcaption>Gráfico de barras</figcaption>
</figure>
<p>La última parte del código corresponde al editor de gráficos de <code>Stata</code>, que permite hacer cambios manualmente. Si queremos asegurar que el gráfico es reproducible podemos seguir estos pasos:</p>
<p><strong>Paso 1</strong>. Abrimos el gráfico en el editor.</p>
<p><strong>Paso 2</strong>. Pulsamos <code>Start recording</code>.</p>
<p><strong>Paso 3</strong>. Realizamos los cambios que queramos (añadir cuadros de texto, flechas, etc.).</p>
<p><strong>Paso 4</strong>. Finalizamos la grabación pulsando <code>End recording</code>.</p>
<p><strong>Paso 5</strong>. Abrimos el archivo <code>.grec</code> creado en el blog de notas (o el propio editor de archivos <code>.do</code> de <code>Stata</code>).</p>
<p><strong>Paso 6</strong>. Copiamos el código y relevante al arhivo <code>.do</code> tras el grafico, eliminamos el <code>.</code> e incluimos antes de cada instrucción <code>gr_edit</code> seguido de un espacio.</p>
<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\grapheditor.png" style="width:40.0%" alt="" /><figcaption>Modificación de gráficos con el editor de <code>Stata</code></figcaption>
</figure>
<h3 id="funciones-de-densidad">Funciones de densidad</h3>
<p>Se trata de uno de los fuertes de <code>Stata</code>, que es complicado de abordar con otro tipo de programas (e.g., Microsoft Excel). En este caso, vamos a representar las funciones de densidad del salario mensual de las personas con educación elemental y educación superior.</p>
<pre class='stata'>. //  Abrimos el archivo
. 
. use "ecv18.dta", clear
(ECV 2016-2018)

. 
. //  Calculamos el salario
. 
. generate rentaasa = rentamonasag + rentamonasag
(5,362 missing values generated)

. generate nmesesasa = nmesesasatc + 0.5*nmesesasatp
(5,578 missing values generated)

. keep if rentaasa > 0 &amp; rentaasa &lt; .
(19,416 observations deleted)

. keep if nmesesasa > 0 &amp; nmesesasa &lt; .
(2,540 observations deleted)

. 
. generate salario = rentaasa/nmesesasa

. label variable salario "Salario bruto (euros/mes)"

. 
. //  Lo expresamos en logaritmos
. 
. generate logsalario = ln(salario)

. label variable logsalario "Salario en logaritmos"

. 
. //  Calculamos los grupos de niveles educativos
. 
. generate educg = 1 if educ == 0 | educ == 100
(10,934 missing values generated)

. replace educg = 2 if educ == 200 | educ == 344 | educ == 353
(3,331 real changes made)

. replace educg = 3 if educ == 300 | educ == 354 | educ == 400 | educ == 450
(2,509 real changes made)

. replace educg = 4 if educ == 500
(5,094 real changes made)

. 
. label variable educg "Educación (agrupada)"

. label define educg_lab 1 "Educación elemental" 2 "Educación básica" 3 "Educación media" 4 "Educación superior"

. label values educg educg_lab

. 
. keep if educg &lt; .
(0 observations deleted)

. 
. //  Eliminamos valores perdidos
. 
. keep if logsalario &lt; . | educg &lt; .
(0 observations deleted)

. 
. //  Elaboramos el gráfico de interés
. 
. twoway ///
> (kdensity logsalario if educg == 1 [aweight = peso]) ///
> (kdensity logsalario [aweight = educg == 4]), ///
> ytitle("Densidad", size(vsmall) margin(sides)) ylabel(, labsize(vsmall) format(%02,1fc)) ///
> xtitle("Salario bruto en logaritmos (euros/mes)", size(vsmall) margin(top_bottom)) xlabel(, labsize(vsmall)) ///
> title("Función de densidad del salario" "por nivel educativo en España (2018)", size(small) margin(top_bottom)) ///
> legend(on order(1 "Educación elemental" 2 "Educación superior") cols(1) size(vsmall) position(10) ring(0))
</pre>

<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\kdensity.png" style="width:40.0%" alt="" /><figcaption>Gráfico de barras</figcaption>
</figure>
<h1 id="elaboración-de-mapas">4. Elaboración de mapas</h1>
<p>No es, desde luego, el mejor software para esta tarea, pero, sin embargo, existen algunos comandos que pueden ser muy útiles para usuarios poco exigentes (e.g., investigadores en Ciencias Sociales para los que los mapas no son centrales). Existen varios paquetes para esta tarea, como <code>spmap</code> o <code>geo2xy</code>. En este curso, emplearemos el primero de ellos, <code>spmap</code>. Podemos encontrar una buena introducción al uso de este paquete en <span class="citation" data-cites="Wiedemann2020">Wiedemann (2020)</span> Este paquete requiere la instalación adicional de <code>shp2dta</code> y <code>mif2dta</code>. Así, podemos instalar todos los parquetes necesarios de la siguiente forma:</p>
<pre class='stata'>. ssc install spmap
checking spmap consistency and verifying not already installed...
all files already exist and are up to date.

. ssc install shp2dta
checking shp2dta consistency and verifying not already installed...
all files already exist and are up to date.

. ssc install mif2dta
checking mif2dta consistency and verifying not already installed...
all files already exist and are up to date.
</pre>
<p>Con <code>spmap</code>, <code>Stata</code> puede manejar dos tipos de formatos gráficos: <strong>ESRI shapefiles</strong> (incluye un archivo <code>.shp</code> de coordenadas, un archivo <code>.shx</code>, un índice y un archivo <code>.dbf</code>, con códigos) y el <strong>MapInfo Interchange Format files</strong> (que comprende un archivo <code>.mif</code> con las coordenadas y un archivo <code>.mid</code> con los códigos). El manejo del primer tipo de formato es, con frecuencia, más sencillo. Para explorar las capacidades de <code>Stata</code> en esta área, vamos a realizar un mapa de España por Comunidades Autónomas con su tasa de pobreza.</p>
<p>Procedemos en varios pasos.</p>
<p><strong>Paso 1</strong>. Buscamos los <strong>ESRI shapefiles</strong> que precisamos. En este caso, podemos encontrarlos en muchos lugares. El mapa “oficial” se encuentra en el (Centro Nacional de Información Geográfica](http://centrodedescargas.cnig.es/CentroDescargas/documentos/atom/au/lineas_limite_gml.zip], los copiamos a la carpeta de trabajo y descomprimimos la carpeta. Nuestro interés radica en la carpeta <code>Recintos autonómicos: poligonos_autonomicas_inspire_peninbal_ etrs89</code>. Nos vamos a limitar a la península, por simplicidad. Extraemos sus archivos y los situamos en la carpeta de trabajo.</p>
<p><strong>Paso 2</strong>. Creamos los archivos de atributos y coordenadas con la siguiente instrucción:</p>
<pre class='stata'>. shp2dta using "recintos_autonomicas_inspire_peninbal_etrs89", data("ccaa_attr.dta") coord("ccaa_coord.dta") genid(stid) gencentroids(cc) 
> replace
type: 5
</pre>
<p><strong>Paso 3</strong>. Con nuestra base de datos, generamos un archivo que podamos unir al de atributos empleando la variable <code>stid</code> del fichero <code>ccaa_attr.dta</code>. Prescindimos, además, de Ceuta y Melilla, por simplicidad.</p>
<pre class='stata'>. //  Abrimos el archivo
. 
. use "ecv18.dta", clear
(ECV 2016-2018)

. 
. //  Calculamos los estadísticos de interés
. 
. collapse (mean) hpobreza [pweight = peso], by(region)

. 
. //  Expresamos la tasa de pobreza en tanto por 100
. 
. replace hpobreza = 100*hpobreza
(19 real changes made)

. 
. //  Eliminamos Canarias
. 
. drop if region == 19
(1 observation deleted)

. 
. //  Recodificamos las regiones para enlazar con la variable stid de ccaa_attr.dta
. 
. recode region (1=10) (2=18) (3=16) (4=6) (5=7) (6=5) (7=19) (8=9) (9=15) (10=14) (11=11) (12=13) (13=12) (14=17) (15=20) (16=8) (17=3) (1
> 8=4)
(region: 17 changes made)

. 
. //  Eliminamos las etiquetas de los valores para evitar confusiones
. 
. label drop region_lab

. 
. //  Cambiamos el nombre de la variable de enlace
. 
. rename region stid

. 
. //  Eliminamos las regiones que no queremos representar
. 
. drop if stid &lt;= 4
(2 observations deleted)

. 
. save "ccaa_pobreza.dta", replace
file ccaa_pobreza.dta saved
</pre>
<p><strong>Paso 4</strong>. De forma opcional, podemos crear un archivo para etiquetar las regiones.</p>
<pre class='stata'>. //  Creamos un archivo con etiquetas.
. 
. use "ccaa_attr.dta", clear

. merge 1:1 stid using "ccaa_pobreza.dta", keep(match)

    Result                           # of obs.
    ─────────────────────────────────────────
    not matched                             0
    matched                                16  (_merge==3)
    ─────────────────────────────────────────

. keep stid hpobreza x_cc y_cc

. format hpobreza %02,1fc

. save "ccaa_labels.dta", replace
file ccaa_labels.dta saved
</pre>
<p><strong>Paso 5</strong>. Realizamos una fusión de nuestro archivo <code>ccaa_pobreza.dta</code> con el archivo <code>ccaa_attr.dta</code> empleando como enlace la variable <code>stid</code>.</p>
<pre class='stata'>. //  Abrimos el archivo ccaa_attr.dta
. 
. use "ccaa_attr.dta", clear

. 
. //  Realizamos la fusión con merge
. 
. merge 1:1 stid using "ccaa_pobreza.dta", keep(match)

    Result                           # of obs.
    ─────────────────────────────────────────
    not matched                             0
    matched                                16  (_merge==3)
    ─────────────────────────────────────────

. 
</pre>
<p><strong>Paso 6</strong>. Creamos el gráfico con <code>spmap</code>.</p>
<pre class='stata'>. //  Creamos el mapa
. 
. spmap hpobreza using "ccaa_coord.dta", ///
> id(stid) fcolor(Reds) ///
> ocolor(black ..) osize(thin ..)  ///
> legend(size(vsmall) position(5) label(2 "0-10") label(3 "10-20") label(4 "20-30") label(5 "30-40")) ///
> clmethod(custom)  clbreaks(0 10 20 30 40) ///
> legtitle("Tasa de pobreza (%)") ///
> title("Tasa de pobreza en España por CCAA (2018)", size(small)) name("pobreza2018", replace) ///
> label(data("ccaa_labels.dta") label(hpobreza) xcoord(x_cc) ycoord(y_cc) color(black) size(vsmall) length(4)) ///
> freestyle yscale(off) xscale(off)

. 
. //    Usamos el editor de Stata para ajustar el tamaño
. 
. gr_edit style.editstyle declared_ysize(5) editcopy

. gr_edit style.editstyle declared_xsize(7) editcopy
</pre>

<figure>
<img src="D:\Dropbox\curso_stata\jianton.github.io\map.png" style="width:70.0%" alt="" /><figcaption>Mapa con el paquete <code>spmap</code></figcaption>
</figure>
<h1 id="references">References</h1>

<div id="refs" class="references hanging-indent" role="doc-bibliography">
<div id="ref-Cox2014">
<p>Cox, N. J. (2014). <em>Speaking stata graphics</em>. Stata Press.</p>
</div>
<div id="ref-Escobar2012">
<p>Escobar, M., Fernández-Macías, E., &amp; Bernardi, F. (2012). <em>Análisis de datos con stata</em> (2nd ed.). Centro de Investigaciones Sociólogicas.</p>
</div>
<div id="ref-Jann2018a">
<p>Jann, B. (2018a). Customizing Stata graphs made easy (part 1). <em>Stata Journal</em>, <em>3</em>(18), 491–502.</p>
</div>
<div id="ref-Jann2018b">
<p>Jann, B. (2018b). Customizing Stata graphs made easy (part 2). <em>Stata Journal</em>, <em>4</em>(19), 786–802.</p>
</div>
<div id="ref-Mitchell2012">
<p>Mitchell, M. N. (2012). <em>A visual guide to Stata graphics</em> (3rd ed.). Stata Press.</p>
</div>
<div id="ref-Schwabish2014">
<p>Schwabish, J. A. (2014). An economists guide to visualizing data. <em>Journal of Economic Perspectives</em>, <em>28</em>(1), 209–234.</p>
</div>
<div id="ref-Wiedemann2020">
<p>Wiedemann, V. (2020). <em>Creating maps in Stata</em> [Coder’s corner]. Centre for the Study of African Economy. <a href="https://www.csae.ox.ac.uk/materials/coderscorner/1037/coderscornerht20week2fm.pdf">https://www.csae.ox.ac.uk/materials/coderscorner/1037/coderscornerht20week2fm.pdf</a></p>
</div>
</div>
</body>
</html>
